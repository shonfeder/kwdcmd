#+TITLE: Kwdcmd: Keywords for Cmdliner CLIs

/A lightweight and partial porcelain around [[https://erratique.ch/software/cmdliner} ][Cmdliner]]/

Cmdliner is a powerful library for composable command line interfaces, but it is
relatively low level.  Most users have to look up the documentation every time
they want to use the combinators correctly.

This thin wrapper library is best thought of as an executable cookbook for
common Cmdliner usage patterns, or as programmatic documentation encoded in the
type level.

The recipes are divided between module namespaces and the functions are
documented with named arguments.

This library is in early stages, and is growing as the need for patterns arises .

* Ideals

- Remain true to Cmdliner's compositional principles (no side-effecting
  shortcuts or other chicanery).
- Unfamiliar users should be able to write their CLIs within 10 minutes of
  reading the docs.
- Familiar users should find everything they need to know to write clean,
  expressive CLI's self-documented in the type signatures of the library's
  modules and functions.

Please [[https://github.com/shonfeder/kwdcmd/issues/new ][open an issue]] or [[https://github.com/shonfeder/kwdcmd/blob/master/CONTRIBUTING.org][add a feature]] if you think the library could be improved
to better meet these ideals.

* Installation

Pin to master with [[https://opam.ocaml.org/doc/Install.html][opam]] via

#+begin_src sh
opam pin https://github.com/shonfeder/kwdcmd.git
#+end_src

Or append =#0.0.n= to the URL to pin a specific release.

* Examples

** Single commands

An interface for a simple project generation tool:

#+begin_src ocaml
open Kwdcmd

(** Application configuration *)
type kind =
  | Bin
  | Lib

type config =
  { name : string
  ; kind : kind
  }

(** Application executor *)
let run : config -> unit = fun _config -> print_endline "TODO"

(** CLI entrypoint *)
let () =
  Exec.run ~name:"My application" ~version:"0.0.1" ~doc:"project generator"
  @@ let+ name =
       Required.pos
         "NAME"
         ~conv:Arg.string
         ~nth:0
         ~doc:"The name of the new project"
         ()
     and+ kind =
       Optional.(
         flag_choice
           ~default:Bin
           [ c ~name:"bin" Bin ~doc:"create an executable binary"
           ; c ~name:"lib" Lib ~doc:"create a library"
           ])
     in
     run { name; kind }
#+end_src

** Sub commands

A sub-command interface to a simple utility for looking up emoji:

#+begin_src ocaml
open Kwdcmd

let () =
  Exec.select
    ~name:"emojitsu"
    ~version:"0.0.1"
    ~doc:"Techniques for dealing with emoji"
    [ ( cmd
          ~name:"find-name"
          ~doc:"Find the name of an emoji given its unicode"
      @@ let+ unicode = Required.pos "UNICODE" ~conv:Arg.string ~nth:0 () in
         Lib.Github.lookup_name unicode )
    ; ( cmd
          ~name:"find-unicode"
          ~doc:"Find the unicode of an emoji given its name"
      @@ let+ name = Required.pos "EMOJI_NAME" ~conv:Arg.string ~nth:0 () in
         Lib.Github.lookup_unicode name )
    ; ( cmd
          ~name:"emojify"
          ~doc:
            "Replace all names of the form :emoji_name: with the corresponding \
             unicode in the given file"
      @@ let+ name =
           Required.pos
             "FILE"
             ~conv:Arg.(conv (Fpath.of_string, Fpath.pp))
             ~nth:0
             ()
         in
         Lib.Github.emojify name )
    ]
#+end_src
